apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext {
    // Maven metadata
    publishedGroupId = 'com.romanpulov.library.view'
    libraryName = 'library'
    // Save yourself a head ache, and set this equal to the name of the Android Studio library
    // module. The artifact name needs to match the name of the library.
    artifact = 'library'

    libraryDescription = 'Roman Pulov visual library for Android'
    libraryVersion = '1.1.7'
}

android {
    namespace = "com.romanpulov.library.view"

    defaultConfig {
        compileSdk 34
        minSdkVersion 14
        targetSdkVersion 34
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
        }
        singleVariant('debug') {
            withSourcesJar()
        }
    }

    lint {
        abortOnError true
    }
}

dependencies {
    // Unit testing dependencies
    testImplementation 'junit:junit:4.13.2'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = publishedGroupId
                artifactId = artifact
                version = libraryVersion
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = publishedGroupId
                artifactId = 'library-debug'
                version = libraryVersion
            }
        }
    }
}